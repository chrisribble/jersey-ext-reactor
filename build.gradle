plugins {
	id 'com.github.ben-manes.versions' version '0.53.0'
	id 'se.solrike.sonarlint' version '2.2.0' apply false
	id 'com.github.spotbugs' version '6.4.8' apply false
	id "net.thebugmc.gradle.sonatype-central-portal-publisher" version "1.2.4" apply false
}

version = '0.0.6'

wrapper {
	gradleVersion = '9.3'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'jacoco'
	apply plugin: "se.solrike.sonarlint"

	group = 'io.github.chrisribble'
	version = rootProject.version

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		sonarlintPlugins libs.sonar.java.plugin
	}

	java {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}

	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) { task ->
			options.compilerArgs.add('-Werror')
			options.compilerArgs.add('-Xlint:all')
			options.compilerArgs.add('-Xlint:-processing')
		}
		tasks.withType(VerificationTask) { task ->
			if (task.name.startsWith("spotbugs")) {
				task.reports {
					html.required = true
				}
			}
		}
	}
}
